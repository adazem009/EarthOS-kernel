// EarthOS kernel
// 2020 - adazem009
//
// Functions
define/"PANIC"
{
	getitem/"arg_1",1/"panicdesc"
	continue/"parts/panic.smc"/panicdesc
	print/"Kernel panic failed! Your kernel may be corrupted.",\n
	exit
}
print/"[ ",%uptime," ] ","Loading EarthOS kernel...",\n
getlocation/"KERNEL_ROOT"
cd/KERNEL_ROOT
// Process inputs
if/[arg_count > 1]
	PANIC/"Only one argument can be passed to the kernel."
endif
warp
	setlist/"KERNEL_INPUTS"
	getlistlength/"arg_1"/"inputs"
	set/"i",0
	while/[i < inputs]
		calc/"i=i+1"
		getitem/"arg_1",i/"input"
		getlength/input/"inputlen"
		setlist/"inputparts"
		set/"inputpart",""
		set/"i2",0
		while/[i2 < inputlen]
			calc/"i2=i2+1"
			getletter/input,i2/"letter"
			if/[letter == "="]
				append/inputpart/"inputparts"
				set/"inputpart",""
			else
				set/"inputpart",inputpart,letter
			endif
		endloop
		getitem/"inputparts",1/"setname"
		getitem/"inputparts",2/"inputvalue"
		set/"setname","KERNEL_INPUT_",setname
		print/"[ ",%uptime," ] ","kernel input: ",setname,"=",inputvalue,\n
		set/setname,inputvalue
	endloop
endwarp
// Setup RAM fs
format/0
renamepart/0,"EarthOS-RAM"
run/"parts/copyr.smc"/"wait"/KERNEL_ROOT,"~ram/kernel"
set/"KERNEL_ROOT","~ram/kernel"
set/"continue",KERNEL_ROOT,"/continue.smc"
continue/continue
PANIC/"Failed to execute continue script."
